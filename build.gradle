plugins {
	id 'org.springframework.boot' version '2.2.0.RELEASE'
	id 'io.spring.dependency-management' version '1.0.8.RELEASE'
	id 'java'
	id 'org.hidetake.swagger.generator' version '2.18.1'
}

group = 'br.com.cvc'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-jersey'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    compile 'io.swagger.core.v3:swagger-annotations:2.0.7'
    compile('org.springframework.boot:spring-boot-starter-webflux')
    compile 'io.springfox:springfox-swagger2:2.9.2'
    swaggerCodegen 'org.openapitools:openapi-generator-cli:3.3.4'
    
    compile 'org.modelmapper:modelmapper:2.3.0'
    
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

swaggerSources {
  DesafioCVC {
    inputFile = file("$rootDir/src/main/resources/api/DesafioCVC.yaml")
    code {
      language = 'spring'
      configFile = file("$rootDir/src/main/resources/configSwagger.json")
	  components = ['models','apis','supportingFiles']
    }
  }
}

task deleteControllerFiles(type: Delete) {
    delete fileTree("${swaggerSources.DesafioCVC.code.outputDir}") {
        include "**/*Controller.java" 
    }
}

test {
	useJUnitPlatform()
}

// Gerar os objetos do YAML e Deletar os controllers que ser√£o implementados manualmente.
compileJava.dependsOn deleteControllerFiles
deleteControllerFiles.dependsOn swaggerSources.DesafioCVC.code

sourceSets.main.java.srcDir "${swaggerSources.DesafioCVC.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${swaggerSources.DesafioCVC.code.outputDir}/src/main/resources"